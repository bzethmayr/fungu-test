final String devId = 'bzethmayr'

publishing {
    repositories {
        if (isLocal(version)) {
            mavenLocal()
        } else {
            mavenCentral()
        }
    }
    publications {
        maven(MavenPublication) {
            groupId = rootProject.group
            artifactId = rootProject.name
            version = rootProject.version

            from components.java

            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
            pom {
                name = rootProject.name
                description = rootProject.description
                url = "https://github.com/${devId}/${rootProject.name}"
                licenses {
                    license {
                        name = 'The 2-Clause BSD License'
                        url = '/license.md'
                    }
                }
                developers {
                    developer {
                        id = devId
                        name = 'Ben Zethmayr'
                        email = 'gribnitt@gmail.com'
                    }
                }
                scm {
                    connection = "scm:git:https://github.com/${devId}/${rootProject.name}.git"
                    developerConnection = "scm:git:https://github.com/${devId}/${rootProject.name}.git"
                    url = "https://github.com/${devId}/${rootProject.name}/"
                }
            }
        }
    }
}

tasks.withType(Jar).configureEach {
    dependsOn test
    dependsOn javadoc
}

tasks.withType(PublishToMavenRepository).configureEach {
    enabled = !isLocal(version)
}